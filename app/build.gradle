apply plugin: 'com.android.application'

// http://stackoverflow.com/questions/28498688/gradle-script-to-autoversion-and-include-the-commit-hash-in-android
def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitStatus = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'status'
        standardOutput = stdout
    }
    return stdout.toString().trim().replace('\\', '\\\\').replace('\n', '\\n').replace('"', '\\"')
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.outerworldapps.hsiwatch"
        minSdkVersion 25
        targetSdkVersion 28
        versionCode 10200
        versionName "1.2"
        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
        buildConfigField "String", "GitStatus", "\"${getGitStatus()}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.google.android.support:wearable:2.7.0'
    implementation 'com.google.android.gms:play-services-wearable:17.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.wear:wear:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'ch.acra:acra:4.8.5'
    compileOnly 'com.google.android.wearable:wearable:2.7.0'
}
